plugins {
    id 'java'
    id "io.freefair.lombok" version "8.14"
    id "run.halo.plugin.devtools" version "0.6.1"
}

group = 'run.halo.feed'
version = rootProject.version

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 21
    options.encoding = 'UTF-8'
}

jar {
    archiveBaseName.set("plugin-feed")
    dependsOn(":api:jar")
}

dependencies {
    implementation project(':api')
    compileOnly 'run.halo.app:api'
    implementation 'org.dom4j:dom4j:2.1.4'
    implementation('org.apache.commons:commons-text:1.10.0') {
        // Because the transitive dependency already exists in halo.
        exclude group: 'org.apache.commons', module: 'commons-lang3'
    }

    testImplementation 'run.halo.app:api'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('processUiResources', Copy) {
    from project(':ui').layout.buildDirectory.dir('dist')
    into layout.buildDirectory.dir('resources/main/console')
    dependsOn project(':ui').tasks.named('assemble')
    shouldRunAfter tasks.named('processResources')
}

tasks.named('classes') {
    dependsOn tasks.named('processUiResources')
}

test {
    useJUnitPlatform()
}

halo {
    version = '2.21.3'
    debug = true
}